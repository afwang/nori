apply plugin: 'com.android.application'

android {
    compileSdkVersion 20
    buildToolsVersion "20.0.0"

    defaultConfig {
        applicationId "com.cuddlesoft.nori"
        minSdkVersion 10
        targetSdkVersion 20
        versionCode 1
        versionName "2.0.0-${gitRevision()}"
        testApplicationId "com.cuddlesoft.nori.test"
        testInstrumentationRunner "android.test.InstrumentationTestRunner"
    }
    buildTypes {
        release {
            runProguard true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        // Enable Java 1.7 features.
        sourceCompatibility JavaVersion.VERSION_1_7;
        targetCompatibility JavaVersion.VERSION_1_7;
    }
}

dependencies {
    // Support libraries.
    compile 'com.android.support:appcompat-v7:20.0.0'
    compile 'com.android.support:support-v4:20.0.0'

    // Third-party libraries.
    // An HTTP+SPDY client for Android and Java applications. http://square.github.io/okhttp/
    compile 'com.squareup.okhttp:okhttp:2.0.0'
    compile 'com.squareup.okhttp:okhttp-urlconnection:2.0.0'
    // A powerful image downloading and caching library for Android http://square.github.io/picasso/
    compile 'com.squareup.picasso:picasso:2.3.3'
    // Utility library for easier unit testing.
    androidTestCompile 'org.easytesting:fest-assert-core:2.0M10'

    // Project dependencies.
    compile project(':norilib') // https://github.com/vomitcuddle/norilib
    compile project(':SearchViewAllowEmpty') // https://github.com/vomitcuddle/SearchViewAllowEmpty

    // *.jar files.
    compile fileTree(dir: 'libs', include: ['*.jar'])
}

// Used to append Git commit hash to version names.
def gitRevision() {
    def cmd = "git rev-parse --short HEAD"
    return cmd.execute().text.trim()
}